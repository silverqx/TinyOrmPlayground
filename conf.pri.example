# Dependencies include and library paths
# ---

# range-v3 library (header only)
# Arch   - pacman -S range-v3
# Gentoo - emerge dev-cpp/range-v3
# Ubuntu - apt install librange-v3-dev

# nlohmann_json library (header only)
# Arch   - pacman -S nlohmann-json
# Gentoo - emerge dev-cpp/nlohmann_json
# Ubuntu - apt install nlohmann-json3-dev

TINY_MAIN_DIR = $$quote($$PWD/../../TinyOrm)
TINY_TINYORM_BUILDS_DIR = $$quote($$TINY_MAIN_DIR/TinyOrm-builds-qmake)

# TinyORM header files
INCLUDEPATH += $$quote($$TINY_MAIN_DIR/TinyORM/include/)

# MinGW
win32-g++|win32-clang-g++ {
    # Includes
    # nlohmann-json, range-v3 and tabulate
    INCLUDEPATH += $$quote(C:/msys64/home/xyz/vcpkg/installed/x64-mingw-dynamic/include/)
    QMAKE_CXXFLAGS += -isystem $$shell_quote(C:/msys64/home/xyz/vcpkg/installed/x64-mingw-dynamic/include/)

    # Libraries
    # TinyORM library path
    # Support gcc and clang
    # build-TinyOrm-Desktop_Qt_5_15_2_MSYS2_UCRT64_clang_64bit-Debug
    clang: LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_MSYS2_UCRT64_clang_64bit-$${TINY_RELEASE_TYPE_CAMEL}/src$${TINY_BUILD_SUBFOLDER}/)
    else:gcc: LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_MSYS2_UCRT64_64bit-$${TINY_RELEASE_TYPE_CAMEL}/src$${TINY_BUILD_SUBFOLDER}/)

    # Use faster linker
    QMAKE_LFLAGS *= -fuse-ld=lld
}
else:win32-msvc {
    # Includes
    # nlohmann-json, range-v3 and tabulate
    INCLUDEPATH += $$quote(E:/xyz/vcpkg/installed/x64-windows/include/)

    # Libraries
    # TinyORM library path
    LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_MSVC2019_64bit-$${TINY_RELEASE_TYPE_CAMEL}/src$${TINY_BUILD_SUBFOLDER}/)
}
else:unix {
    # Includes
    # nlohmann-json, range-v3 and tabulate
    QMAKE_CXXFLAGS += -isystem $$quote(/home/xyz/vcpkg/installed/x64-linux/include/)

    # Libraries
    # TinyORM library path
    # Link ccache build against a ccache build
    contains(OUT_PWD, .*ccache.*): TINY_CCACHE_BUILD = _ccache
    else: TINY_CCACHE_BUILD =
    # Support gcc and clang
    clang: LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_clang$${QMAKE_CLANG_MAJOR_VERSION}_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src$${TINY_BUILD_SUBFOLDER}/)
    else:gcc: LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_GCC_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src$${TINY_BUILD_SUBFOLDER}/)

    # Use faster linkers
    clang: CONFIG *= use_lld_linker
    else: CONFIG *= use_gold_linker
}
