# Dependencies include and library paths
# ---

TINY_MAIN_DIR = $$quote($$PWD/../../TinyOrm)
TINY_TINYORM_BUILDS_DIR = $$quote($$TINY_MAIN_DIR/TinyOrm-builds)

# TinyORM header files
INCLUDEPATH += $$quote($$TINY_MAIN_DIR/TinyORM/include)

win32-g++* {
}
else:win32-msvc* {
    # Includes
    # range-v3
    INCLUDEPATH += $$quote(E:/xyz/vcpkg/installed/x64-windows/include)

    # Libraries
    # TinyORM library path
    LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_MSVC2019_64bit-$${TINY_RELEASE_TYPE_CAMEL}/src/$${TINY_RELEASE_TYPE}/)
}
else:unix {
    # Includes
    # range-v3
    QMAKE_CXXFLAGS += -isystem $$quote(/home/xyz/vcpkg/installed/x64-linux/include)

    # Libraries
    # TinyORM library path
    # Link ccache build against a ccache build
    contains(OUT_PWD, .*ccache.*): TINY_CCACHE_BUILD = _ccache
    else: TINY_CCACHE_BUILD =
    # Support gcc, clang11, and clang12
    clang {
        equals(QMAKE_CLANG_MAJOR_VERSION, 11): LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_clang11_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src/)
        else:equals(QMAKE_CLANG_MAJOR_VERSION, 12): LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_clang12_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src/)
    }
    else:gcc: LIBS += $$quote(-L$$TINY_TINYORM_BUILDS_DIR/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_GCC_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src/)

    # Use faster linkers
    clang: CONFIG *= use_lld_linker
    else: CONFIG *= use_gold_linker
}
