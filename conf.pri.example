# Dependencies include and library paths
# ---

win32-g++* {
}
else:win32-msvc* {
    # Includes
    # range-v3
    INCLUDEPATH += $$quote(E:/xyz/vcpkg/installed/x64-windows/include)

    # Libraries
    # TinyORM library path
    LIBS += $$quote(-L$$PWD/../../TinyOrm/TinyOrm-builds/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_MSVC2019_64bit-$${TINY_RELEASE_TYPE_CAMEL}/src/$${TINY_RELEASE_TYPE}/)
}
else:unix {
    # Includes
    # range-v3
    INCLUDEPATH += $$quote(/home/xyz/vcpkg/installed/x64-linux/include)

    # Libraries
    # TinyORM library path
    # ccache build link against a ccache build
    contains(OUT_PWD, .*ccache.*): TINY_CCACHE_BUILD = _ccache
    else: TINY_CCACHE_BUILD =
    # support gcc, clang11, and clang12
    clang {
        equals(QMAKE_CLANG_MAJOR_VERSION, 11): LIBS += $$quote(-L$$PWD/../../TinyOrm/TinyOrm-builds/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_clang11_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src/)
        else:equals(QMAKE_CLANG_MAJOR_VERSION, 12): LIBS += $$quote(-L$$PWD/../../TinyOrm/TinyOrm-builds/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_clang12_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src/)
    }
    else:gcc: LIBS += $$quote(-L$$PWD/../../TinyOrm/TinyOrm-builds/build-TinyOrm-Desktop_Qt_$${TINY_QT_VERSION_UNDERSCORED}_GCC_64bit$${TINY_CCACHE_BUILD}-$${TINY_RELEASE_TYPE_CAMEL}/src/)

    # Use faster linkers
    clang: CONFIG *= use_lld_linker
    else: CONFIG *= use_gold_linker
}
